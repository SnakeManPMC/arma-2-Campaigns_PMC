_eastEnemy=_this select 0
_westEnemy=_this select 1
_guerEnemy=_this select 2
_civEnemy=_this select 3
_scanInterval=_this select 4
CoC_AD_S=0
CoC_ADMod=[]
CoC_ADUnits=[]

_cntr=2

_sides=[]
?CoC_Maintrig==CoC_Maintrig:goto"l1"
Player globalchat{ERROR: Trigger named "CoC_MainTrig" missing, CoC AI on Demand off.}
exit

#l1
?player==player:""exec"CoC_AD\progress.sqs"
?!local CoC_MainTrig:exit

_to=15
CoC_ADwait=true
_maincond=preprocessfile"CoC_AD\maincond.sqf"

@_time>_to||count list CoC_MainTrig>0
;______________ WAIT FOR TRIGGER TO SETTLE
_to=_time+.6
#w
_c=count list CoC_MainTrig
@_c<count list CoC_MainTrig||_to<_time
?_to<_time:goto"trigRdy"
~.1
_to=_time+.5
goto"w"

#trigRdy

_j=(count CoC_ADGrps)-1
?COC_ADdebug:Player globalchat{Stage 1. record group's, waypoints and sort into side arrays}
CoC_AD_s1=_j+1;CoC_AD_s=1;PublicVariable"CoC_AD_s";PublicVariable"CoC_AD_s1"
;____ grab waypoints, groups and sort according to side
_gt=0
#l2
	?_j<0:goto"l2d"
	_grp=CoC_ADGrps select _j
	_g=group(_grp select 0)
	_wpt=getWPpos[_g,0];_wpts=[_wpt];_i=1;_gt=_gt+1
	while{{_x==0}count _wpt<3}do{_wpt=getWPpos[_g,_i];_wpts=_wpts+[+_wpt];_i=_i+1;}
	_wpts set[_i-1,0];_wpts=_wpts-[0]
	_side=side _g;_sarr=format["CoC_AD%1Grps",_side]
	?!(_side in _sides):_sides=_sides+[_side];call(_sarr+{=[];})
	; group,dist,side,[waypoints],spawned,[members]
	call format[{%1=%1+[[_g,_grp select 1,_side,+_wpts,false,[],_grp select 2,_grp select 3]];},_sarr]
	_j=_j-1
	goto"l2"
#l2d



?COC_ADdebug:Player globalchat{Stage 2. do preliminary analysis on which groups to put in stasis}
;____ put selected groups in stasis initially (detect their proper status)
CoC_AD_s2=0;PublicVariable"CoC_AD_s2";CoC_AD_s=2;PublicVariable"CoC_AD_s";_t=0
_i=(count _sides)-1
; ___ each AD side
#l3
	?_i<0:goto"l3d"
	_sarr=call format["CoC_AD%1Grps",_sides select _i];_earr=call format["_%1Enemy",_sides select _i];_j=(count _sarr)-1
	; ___ each group in AD side
	#l4	
		?_j<0:goto"l4d"
		_grp=_sarr select _j;_units=+(list CoC_MainTrig);_k=(count _units)-1;CoC_AD_s2=CoC_AD_s2+1;_d=999999
		?CoC_AD_s2 mod 5==0:PublicVariable"CoC_AD_s2"
		call _maincond
		?!(_grp select 4):_t=_t+1
		_j=_j-1
		goto"l4"
	#l4d
	_i=_i-1
	goto"l3"
#l3d



;____ put selected groups in stasis
?COC_ADdebug:Player globalchat{Stage 3. put selected groups in stasis, save all properties}
CoC_AD_s=3;PublicVariable"CoC_AD_s";CoC_AD_s2=_t;PublicVariable"CoC_AD_s2";CoC_AD_s3=0
_i=(count _sides)-1
; ___ each AD side
#l6
	?_i<0:goto"l6d"
	_sarr=format["CoC_AD%1Grps",_sides select _i]
	_j=(count call _sarr)-1
	; ___ each group in AD side
	#l7	
		?_j<0:goto"l7d"
		_grp=(call _sarr)select _j;(_grp select 0)setCombatMode(_grp select 7)
		;?COC_ADdebug:player globalchat format["Stage 3. action %1 leave on map %2",_grp select 0,_grp select 4]
		if(_grp select 4)then{call(_sarr+{ set[_j,0];});}else{CoC_AD_s3=CoC_AD_s3+1;PublicVariable{CoC_AD_s3};CoC_ADwait=false;[_grp]exec"CoC_AD\inStasis.sqs";}
		@CoC_ADwait
		?count(_grp select 5)==0:call(_sarr+{ set[_j,0];})
		_j=_j-1
		goto"l7"
	#l7d
	call format["%1=%1 -[0];",_sarr]
	_i=_i-1
	goto"l6"
#l6d
CoC_ADready=true
~2
CoC_AD_s=4;PublicVariable"CoC_AD_s";publicvariable"CoC_ADready"
CoC_ADdbg1=0
CoC_ADdbg2=0




?COC_ADdebug:Player globalchat{Stage 4. Monitor to spawn groups in stasis back in.}
_pt=0
publicvariable"CoC_ADready"

#main
~_pt-_time

_pt=_time+_scanInterval
_i=(count _sides)-1
CoC_ADdbg1=CoC_ADdbg1+1
#l8
?_i<0:goto"l8d"
_sarr=call format["CoC_AD%1Grps",_sides select _i]
_earr=call format["_%1Enemy",_sides select _i]

_j=count _sarr -1
_en=[]
;old {if(side _x in _earr)then{_en=_en+[_x];};}foreach list CoC_MainTrig
{if(side _x in _earr)then{if({Air}countType[_x]==0)then{_en=_en+[_x];};};}foreach list CoC_MainTrig
~.5
#l9	
?_j<0:_i=_i-1;goto"l8"
	_grp=_sarr select _j;
	?_grp select 4:_j=_j-1;goto"l9"
	~.01
	
	_cond=_grp select 1;_d=999999;_k=0
	if(call _cond)then{_k=-2;}else{if(_grp select 6)then{_k=(count _en)-1;while{_k>-1}do{_pos=getpos(_en select _k);{_d=sqrt(((_x select 0)-(_pos select 0))^2+((_x select 1)-(_pos select 1))^2);if(call _cond)then{_k=-1;};}foreach(_grp select 3);_k=_k-1;};};};
	?_k!=-2:_j=_j-1;goto"l9"
	_grp exec CoC_ADfunSpawn
	CoC_ADdbg2=CoC_ADdbg2+1;PublicVariable{CoC_ADdbg2}
	~_cntr
	_j=_j-1
goto"l9"
#l8d
?CoC_ADstop:goto"cleanup"
goto"main"


#cleanup
; ___ each AD side
_i=(count _sides)-1
#l11
	?_i<0:goto"l11d"
	_sarr=call format["CoC_AD%1Grps",_sides select _i]
	{[_x]call CoC_ADfunSpawn;}foreach _sarr
	{_x=nil;}foreach _sarr
	call format["CoC_AD%1Grps=nil;",_sides select _i]
	~2
	_i=_i-1
	goto"l11"
#l11d
player globalchat"CoC_AD off"
CoC_ADstop=false
~(_scanInterval*2)
publicvariable"CoC_ADstop"
